#include "zcommon.acs"
#include "constant.acs"

// Scripts

Script "GAG_ARK" (int tag, int oceanSector, int sector3D) // This script is used to sink the ark
{
    int sector3DZDelta = GetSectorCeilingZ(sector3D, 0, 0) - GetSectorFloorZ(oceanSector, 0, 0) + 1;
    Floor_MoveToValue(tag, NORMAL, 128, 1);
    FloorAndCeiling_LowerByValue(sector3D, NORMAL, sector3DZDelta);
    ChangeFloor(tag, "FWATER1");
}

Script "GAG_DSAREA" (int volume, int location) // Prints the Dark Souls area name on screen
{
    AmbientSound(DSAREA, volume);
    switch (location)
    {
        case CATHEDRAL_ID:
            HudMessage(s:CATHEDRAL_NAME; HUDMSG_FADEINOUT, 0, CR_UNTRANSLATED, 0.5, 0.35, DSAREA_HOLDTIME, DSAREA_FADETIME, DSAREA_FADETIME);
            break;
        
        case MINE_ID:
            HudMessage(s:MINE_NAME; HUDMSG_FADEINOUT, 0, CR_UNTRANSLATED, 0.5, 0.35, DSAREA_HOLDTIME, DSAREA_FADETIME, DSAREA_FADETIME);
            break;
    }
}

Script "GAG_GOULD" (void) // A thoughtful eulogy for Stephen J. Gould
{
    HudMessage(s:"In Loving Memory: Gould\nMay his quotes serve a greater purpose"; HUDMSG_PLAIN, 0, CR_UNTRANSLATED, 0.5, 0.35, 5.0);
}

Script "GAG_DAPPER" (int lineID, int leftActorMoverID, int rightActorMoverID) // Initiates the Dapper Dinosaur scripted cameo scene
{
    //ACS_NamedExecute("MUSIC_CROSSFADE", 0, NONE);
	Floor_LowerToNearest(0, NORMAL);
    Delay(70);
    AmbientSound(DAPPERTHEME, 100);
    Delay(120);
    Thing_Activate(leftActorMoverID);
    Thing_Activate(rightActorMoverID);
    Line_SetBlocking(lineID, 0, BLOCKF_EVERYTHING);
}