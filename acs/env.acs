#include "zcommon.acs"
#include "constant.acs"

// Map variables

bool isDapperDead = FALSE;

// Scripts

Script "MUSIC_CROSSFADE" (int song) // Creates a crossfade between the current song and a new one
{
    int musicVolume = 1.0;

    while(musicVolume > 0)
    {
        musicVolume -= MUSIC_CROSSFADETIME;
        SetMusicVolume(musicVolume);
        Delay(1);
    }

    SetMusicVolume(1.0);
    
    switch (song)
    {
        case NONE:
            SetMusic("");
            break;

        case MUSIC_DEFAULT_ID:
            SetMusic(MUSIC_DEFAULT);
            break;

        case CATMUS_ID:
            SetMusic(CATMUS);
            break;

        case BENTSONG_ID:
            SetMusic(BENTSONG);
            break;
    }
}

Script "ARENA_DAPPER" (int doorTag) // Initiates the Dapper Dinosaur arena fight
{
    // Initial cutscene

    SetMusic(BENTSONG);
    Door_Close(doorTag, NORMAL);
	Floor_LowerByValueTimes8(0, NORMAL, 3);
    Ceiling_CrushRaiseAndStay(DAPPER_ARENA_SECTOR, SLOW, 100, 3);
    TagWait(DAPPER_ARENA_SECTOR);
    isDapperDead = TRUE;
    Thing_SetConversation(KENT_ID, 676);

    // Unleashes the hidden monsters

    Door_Open(DAPPER_ARENA_DOORS_SECTOR, NORMAL);
    Floor_LowerToNearest(DAPPER_ARENA_BARONPIL_SECTOR, NORMAL);

    // Spawns new monsters

    Thing_SpawnFacing(DAPPER_ARENA_CACOSPAWN, T_CACODEMON, FALSE, DAPPER_ARENA_ENEMYID);
    Thing_SpawnFacing(DAPPER_ARENA_PAINSPAWN, T_PAINELEMENTAL, FALSE, DAPPER_ARENA_ENEMYID);

    // After the monsters are killed

    while (ThingCount(T_NONE, DAPPER_ARENA_ENEMYID) > 0)
    {
        Delay(70);
    }
    SetMusic(MUSIC_DEFAULT);
    Door_Open(doorTag, NORMAL);
}

Script 666 (void) // Initiates the Kent arena fight
{
    //SetActivator(0, AAPTR_PLAYER1);

    // Initial cutscene

    Floor_RaiseToCeiling(KENT_ARENA_DOORSECTOR, NORMAL);
    FloorAndCeiling_LowerByValue(KENT_ARENA_CROSSARMS, NORMAL, 96);
    Warp(KENT_ARENA_PLAYERWARP, 0.0, 0.0, 0.0, 0, 0);
    Delay(10);
    ThrustThing(0, 75, TRUE);
    ThrustThingZ(0, 100, 0, 0);

    // Spawn new monsters

    Thing_SpawnFacing(KENT_ARENA_CYBERSPAWN, T_CYBERDEMON, FALSE, KENT_ARENA_ENEMYID);
    Delay(35);
    Thing_SpawnFacing(KENT_ARENA_IMPSPAWN, T_IMP, FALSE, KENT_ARENA_ENEMYID);
    Delay(35);
    Thing_SpawnFacing(KENT_ARENA_SHOTGUNSPAWN, T_SHOTGUY, FALSE, KENT_ARENA_ENEMYID);
    Thing_SpawnFacing(KENT_ARENA_CHAINSPAWN, T_CHAINGUY, FALSE, KENT_ARENA_ENEMYID);
    Thing_SpawnFacing(KENT_ARENA_DEMONSPAWN, T_DEMON, FALSE, KENT_ARENA_ENEMYID);
    Delay(35);
    Thing_SpawnFacing(KENT_ARENA_BARONSPAWN, T_BARON, FALSE, KENT_ARENA_ENEMYID);
    Delay(35);
    Thing_SpawnFacing(KENT_ARENA_CACOSPAWN, T_CACODEMON, FALSE, KENT_ARENA_ENEMYID);
    Thing_SpawnFacing(KENT_ARENA_PAINSPAWN, T_PAINELEMENTAL, FALSE, KENT_ARENA_ENEMYID);
    Delay(70);
    Thing_Spawn(KENT_ARENA_CELLSPAWN, T_BATTERY, 0, 0);
    Thing_Spawn(KENT_ARENA_AMMOSPAWN, T_BACKPACK, 0, 0);
    Thing_Spawn(KENT_ARENA_SHELLSPAWN, T_SHELLBOX, 0, 0);
    Thing_Spawn(KENT_ARENA_MEDKITSPAWN, T_MEDKIT, 0, 0);
    Thing_Spawn(KENT_ARENA_ARMORSPAWN, T_GREENARMOR, 0, 0);
    Thing_Spawn(KENT_ARENA_SOULSPHERESPAWN, T_SOULSPHERE, 0, 0);
    Thing_Spawn(KENT_ARENA_MEGASPHERESPAWN, T_MEGASPHERE, 0, 0);
    Thing_Spawn(KENT_ARENA_INVINCIBLESPAWN, T_INVULNERABILITY, 0, 0);
    
    // After the monsters are killed
    
    while (ThingCount(T_NONE, KENT_ARENA_ENEMYID) > 0)
    {
        Delay(70);
    }
    Thing_SetConversation(KENT_ID, 696);
}

Script "ARK_DOOR" (int id) // Keeps the Ark locked until Dapper is dead
{
    if (!isDapperDead)
    {
        HudMessage(s:ARK_LOCKED; HUDMSG_PLAIN, 0, CR_UNTRANSLATED, 0.5, 0.35, 5.0);
    }
    else if (isDapperDead)
    {
        Door_Raise(0, 16, 150);
    }
}

Script "SECRET_TELE" (int sector, int line, int destination) // The secret teleporter near the grave
{
    HudMessage(s:SECRET_LOOKBEHINDYOU; HUDMSG_PLAIN, 0, CR_UNTRANSLATED, 0.5, 0.35, 5.0);
    ChangeFloor(sector, GATE4);
    Floor_RaiseByValueTimes8(sector, SLOW, 1);
    Sector_SetRotation(sector, 45, 0);
    SetLineSpecial(line, Teleport, destination, 0, 0);
}