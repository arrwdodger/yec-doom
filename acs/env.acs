#include "zcommon.acs"
#include "constant.acs"

// Scripts

Script "MUSIC_CROSSFADE" (int song) // Creates a crossfade between the current song and a new one
{
    int musicVolume = 1.0;

    while(musicVolume > 0)
    {
        musicVolume -= MUSIC_CROSSFADETIME;
        SetMusicVolume(musicVolume);
        Delay(1);
    }

    SetMusicVolume(1.0);
    
    switch (song)
    {
        case NONE:
            SetMusic("");
            break;

        case MUSIC_DEFAULT_ID:
            SetMusic(MUSIC_DEFAULT);
            break;

        case CATMUS_ID:
            SetMusic(CATMUS);
            break;

        case BENTSONG_ID:
            SetMusic(BENTSONG);
            break;
    }
}

Script "ARENA_DAPPER" (void) // Initiates the Dapper Dinosaur arena fight
{
    // Initial cutscene

    SetMusic(BENTSONG);
	Floor_LowerByValueTimes8(0, NORMAL, 3);
    Ceiling_CrushRaiseAndStay(DAPPER_ARENA_SECTOR, SLOW, 100, 3);
    TagWait(DAPPER_ARENA_SECTOR);

    // Unleashes the hidden monsters

    Door_Open(DAPPER_ARENA_DOORS_SECTOR, NORMAL, 0);
    Floor_LowerToNearest(DAPPER_ARENA_BARONPIL_SECTOR, NORMAL);

    // Spawns new monsters

    Thing_SpawnFacing(DAPPER_ARENA_CACOSPAWN, T_CACODEMON, FALSE, DAPPER_ARENA_ENEMYID);
    Thing_SpawnFacing(DAPPER_ARENA_PAINSPAWN, T_PAINELEMENTAL, FALSE, DAPPER_ARENA_ENEMYID);

    // After the monsters are killed

    while (ThingCount(T_NONE, DAPPER_ARENA_ENEMYID) > 0)
    {
        Delay(70);
    }
    SetMusic(MUSIC_DEFAULT);
}

Script "ARK_DOOR" (void) // Keeps the Ark locked until Dapper is dead
{
    if (!CheckActorState(DAPPER_ID, STATE_DEATH))
    {
        HudMessage(s:ARK_LOCKED; HUDMSG_PLAIN, 0, CR_UNTRANSLATED, 0.5, 0.35, 5.0);
    }
    else if (CheckActorState(DAPPER_ID, STATE_DEATH))
    {
        Door_Raise(0, 16, 150, 0);
    }
}

Script "SECRET_TELE" (int sector, int line, int destination)
{
    HudMessage(s:SECRET_LOOKBEHINDYOU; HUDMSG_PLAIN, 0, CR_UNTRANSLATED, 0.5, 0.35, 5.0);
    ChangeFloor(sector, GATE4);
    Floor_RaiseByValueTimes8(sector, SLOW, 1);
    Sector_SetRotation(sector, 45, 0);
    SetLineSpecial(line, Teleport, destination, 0, 0);
}